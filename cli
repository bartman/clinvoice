#!/bin/zsh
# Copyright (c) 2010 Bart Trojanowski <bart@jukie.net>

# CLI uses autoload to load functions defined in a lib subdirectory.
# This code finds that directory and sets functions for autoload.
if [[ -z "${CLI_LIB_DIR}" ]] ; then
echo "# 0 $0"
        local dir0=${0%/*}
        local dirL=${$(readlink -f $0)%/*}
        local sfx=lib/clinvoice
        trydirs=( $dir0/$sfx $dir0/../$sfx $dirL/$sfx $dirL/../$sfx )
        for dir in $trydirs ; do
                if [[ -d $dir ]] ; then
                        CLI_LIB_DIR=$dir
                        break
                fi
        done
        if [[ -z "${CLI_LIB_DIR}" ]] ; then
                echo >&2 "could not find clinvoice lib files, set CLI_LIB_DIR"
                exit 1
        fi
fi
fpath=(${CLI_LIB_DIR} $fpath)
for file in ${CLI_LIB_DIR}/*[^~] ; do
        autoload -U ${file##*/}
done

# parse global parameters
while [[ -n "$1" && "${1[1]}" = "-" ]] ; do
        opt=$1 ; shift
        case "$opt" in
            -h|--help)
                cli-cmd-help
                ;;
            *)
                die "unknown global option '$opt'"
                ;;
        esac
done

# if is no more to parse, bail now
[[ ${#argv} = 0 ]] && cli-cmd-help 1

# handle the command
cli_cmd_name="$1" ; shift
cmd="cli-cmd-$cli_cmd_name"

( whence -w "$cmd" >/dev/null 2>&1 ) || die "unknown command '$cli_cmd_name'"

"$cmd" ${*}
